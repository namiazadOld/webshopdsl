module pages
	imports include/WebShopDSL
	  		libstratego-lib
  			libstratego-gpp
  			libstratego-aterm
  			lib/editor-common.generated
  			generate-wdsl
  			
rules
			
		pg : Shop(shopName, with*, sells, basicType*, productType*, groupByType, groupByProp*) -> 
			$[
				section productPages
				
				[<map(pg);concat-strings>productType*]
				[<map(pgEdit);concat-strings>productType*]
				section leftcategoriesmenu

				define leftsidemenu(){
		
					<div id="accordion"> 
						[<leftMenuByType(|productType*)>groupByType]
						[<map(leftMenuByProperty);concat-strings> groupByProp* ]
					</div> 
				}		
				
				[<listByType(|productType*)> groupByType]
				[<map(listByProperty(|productType*));concat-strings> groupByProp*]
				
			]
		
		pg	:	BasicProduct(name, prop*) -> <createPage(|[], prop*)>name
		
		pg	: 	Product(name, base, prop*) -> <createPage(| base, prop*)>name

		pgEdit	:	BasicProduct(name, prop*) -> <editPage(|[], prop*)>name
		
		pgEdit	: 	Product(name, base, prop*) -> <editPage(| base, prop*)>name
		

		
		complexCRUDPages: l@ListOfComplex(name, ComplexType(typeName, prop*), attr*) -> <createPage(| [], prop*)>typeName
		
		complexCRUDPages: l@ComplexProperty(name, ComplexType(typeName, prop*), attr*) -> <createPage(| [], prop*)>typeName

		complexCRUDPages: l@SimpleProperty(name, type, attr*) -> $[]

		complexEditPages: l@ListOfComplex(name, ComplexType(typeName, prop*), attr*) -> <editPage(| [], prop*)>typeName
		
		complexEditPages: l@ComplexProperty(name, ComplexType(typeName, prop*), attr*) -> <editPage(| [], prop*)>typeName

		complexEditPages: l@SimpleProperty(name, type, attr*) -> $[]
				
				
		baseCheck(|entity) : [] -> $[]
		baseCheck(|entity) : [base] -> $[[basic]]
		with baseProp* := <BasicProductMap> base ;
		basic := <map(pg(|entity))>baseProp*
		
		baseCheck2(|entity) : [] -> $[]
		baseCheck2(|entity) : [base] -> $[[basic]]
		with baseProp* := <BasicProductMap> base;
		basic := <map(fieldDetailsUI(|entity));concat-strings>baseProp*
				
		pg(|entity): SimpleProperty(name, type, attr*) -> $[
				par{ label("[name] "){ input([entity].[name]) } }			
		]
		pg(|entity): ComplexProperty(name, type, attr*) ->  $[
				par{ label("[name] "){ input([entity].[name]) } }			
		]
		pg(|entity): ListOfComplex(name, type, attr*) -> $[
				par{ label("[name] "){ input([entity].[name]) } }			
		]
		
		leftMenuByProperty: GroupByProperty(base, prop) -> $[
	
			<div> 
				<h3><a href="#">output("By [propTypeName]")</a></h3> 
				<div>
								
					<ul>
						for([varname] :[propTypeName] ){
							<li> navigate(listBy[propTypeName]([varname])){ output([varname])} </li>
						}
					</ul>
				</div> 
			</div> 
		]
		with varname:= <lower-case>prop
		with c@ComplexProperty(propName, ComplexType(propTypeName, prop*), attr*) := <GetProperty><concat-strings>[base, ".", prop]
		
		leftMenuByType(|productType*) : None() -> $[]
		leftMenuByType(|productType*): Some(GroupByType()) -> $[
	
			<div> 
				<h3><a href="#">output("By Product")</a></h3> 
				<div>
								
					<ul>
						[<map(leftMenuByTypeContent);concat-strings> productType* ]
					</ul>
				</div> 
			</div> 
		]
		
		leftMenuByTypeContent : BasicProduct(name, prop*) -> $[]
		leftMenuByTypeContent : Product(name, base, prop*) -> $[
			<li> navigate(listBy[name]()){ output("[name]")} </li>
		]
		
		createPage(|base, prop*): name ->  $[define page create[name]() {
			init { }
			var [ varname ]:= [name]{};
			main
			define body(){
				section {
					header {"Create New [name]"}
					form{
						[<baseCheck(|varname)>base]
						[<map(pg(|varname))>prop*]
						submit create([varname]) {"Create" }
					}
				}
			}
			
			action create([varname] : [name])
			{
				
				[varname].save();
				log("[name]Creation Log: " + [varname]);
				message("[name] has been created successfully.");				
				
				return create[name](); 
			}
		}

		define [varname]Detail([varname]: [name])
		{
			<div class="ui-widget ui-widget-container" >
				<h3 class="ui-widget ui-widget-header  ui-corner-top">output([varname].name)</h3>
				
				<div id="cdSummary" class="ui-widget ui-widget-content ui-corner-bottom">
		  		
		  			[<baseCheck2(|varname)>base]
		  			[<map(fieldDetailsUI(|varname));concat-strings>prop*]
	 				navigate(edit[name]([varname])){output("Edit")}
		  		</div>
	  		</div>
		}		
		
		[<map(complexCRUDPages)>prop*]

		[<map(complexEditPages)>prop*]
		
		]
		with varname:= <lower-case>name


		editPage(|base, prop*): name ->  $[
			define page edit[name]([varname] : [name]) {
			init { }
			main
			define body(){
				section {
					header {"Edit [name]"}
					form{
						[<baseCheck(|varname)>base]
						[<map(pg(|varname))>prop*]
						submit create([varname]) {"Create" }
					}
				}
			}
			
			action create([varname] : [name])
			{
				
				[varname].save();
				log("[name]Creation Log: " + [varname]);
				message("[name] has been created successfully.");				
				
				return [varname]Profile([varname]); 
			}
		} ]
		with varname:= <lower-case>name 


		
		fieldDetailsUI(|varname) : a@SimpleProperty(propName, type, attr*) -> <map(ruleSimple1(|varname, propName));concat-strings> attr*
		fieldDetailsUI(|varname) : ComplexProperty(propName, type, attr*) -> <map(ruleComplex1(|varname, propName));concat-strings> attr*	
		fieldDetailsUI(|varname) : ListOfComplex(propName, ComplexType(type, prop*), attr*) -> <map(ruleListOfComplex1(|varname, propName, type));concat-strings> attr*
		
		ruleSimple1 (|varname, propName) : a@AttributeList(attr*, attr) -> result
		with r1 := <map(ruleSimple2(|varname, propName));concat-strings>attr*;
			 r2 := <ruleSimple2(|varname, propName)> attr;
			 result := <concat-strings>[r1, r2]
		ruleSimple2(|varname, propName) : Attribute("summary", "\"true\"") -> $[
	  					par {output("[propName] : " + [varname].[propName]) }	

	  				
		]
		ruleSimple2(|varname, propName) : Attribute(t1, t2) -> $[]
		
		ruleComplex1 (|varname, propName) : a@AttributeList(attr*, attr) -> result
		with r1 := <map(ruleComplex2(|varname, propName));concat-strings>attr*;
			 r2 := <ruleComplex2(|varname, propName)> attr;
			 result := <concat-strings>[r1, r2]
		ruleComplex2(|varname, propName) : Attribute("summary", "\"true\"") -> $[
			if ([varname].[propName] != null)
			{

		  				par{	output("[propName] : " + [varname].[propName].name)}	
	  		}
	  				
		]
		ruleComplex2(|varname, propName) : Attribute(t1, t2) -> $[]
		
		ruleListOfComplex1 (|varname, propName, propType) : a@AttributeList(attr*, attr) -> result
		with r1 := <map(ruleListOfComplex2(|varname, propName, propType));concat-strings>attr*;
			 r2 := <ruleListOfComplex2(|varname, propName, propType)> attr;
			 result := <concat-strings>[r1, r2]
		ruleListOfComplex2(|varname, propName, propType) : Attribute("summary", "\"true\"") -> $[
			if ([varname].[propName] != null)
			{
				<strong>output("List of [propName]:")</strong>
  					for ([value]: [propType] in [varname].[propName])
  					{
  						par{output([value].name)}
  					}
	  		}
	  				
		]
		with value:= $[value]
		ruleListOfComplex2(|varname, propName, propType) : Attribute(t1, t2) -> $[]
		
		
		//Filter the product on special base
		for-each(|b, prop, value) : Product(name, [b], prop*) -> $[
				for(entity :[name] where entity.[prop] == [value])
				{
					[lower]Detail(entity) 
				}
			]
		with lower:= <lower-case>name		
		for-each(|b, prop, value) : Product(name, base, prop*) -> $[]
		for-each(|b, prop, value) : BasicProduct(name, prop*) -> $[]
		

		
		listByProperty(|productType*) : GroupByProperty(basicProduct, prop) -> $[
		define page listBy[propTypeName]([value] : [propTypeName]){
			main()
			define body(){
				<div id="listAccordion">
					[<map(for-each(|basicProduct, prop, value));concat-strings>productType*]
				</div>
			}
		}
		]
		with c@ComplexProperty(propName, ComplexType(propTypeName, prop*), attr*) := <GetProperty><concat-strings>[basicProduct, ".", prop]
		with value := "value"
		
		for-eachType() : Product(name, base, prop*) -> $[
				for(entity :[name])
				{
					[lower]Detail(entity) 
				}
			]
		with lower:= <lower-case>name		
		for-eachType() : BasicProduct(name, prop*) -> $[]
		
		
		createTypeListPage : BasicProduct(name, prop*) -> $[]
		createTypeListPage : Product(name, base, prop*) -> $[
			define page listBy[name](){
				main()
				define body(){
					<div id="listAccordion">
						for(entity :[name])
						{
								[lower]Detail(entity)
						}						
					</div> 
				}
			}
		]
		with lower:= <lower-case>name

		listByType(|productType*) : None() -> $[]
		listByType(|productType*) : Some(GroupByType()) -> $[
			[<map(createTypeListPage);concat-strings>productType*]
		]
		with value := "value"
			
		

		
		
		
		
		
		
		
		
		
		
		
		
		
		
		
		
		